# ------------------------------------------------------------------
# Licensed under the ISC License. See LICENCE in the project root.
# ------------------------------------------------------------------

module LocallyWeightedRegression

using GeoStatsBase

using LinearAlgebra
using NearestNeighbors
using StaticArrays
using Distances

import GeoStatsBase: solve

export LocalWeightRegress

"""
    LocalWeightRegress(varâ‚=>paramâ‚, varâ‚‚=>paramâ‚‚, ...)

Locally weighted regression (LOESS) estimation solver.

## Parameters

* `weightfun` - Weighting function (default to `exp(-h^2/2)`)
* `distance`  - A distance from Distances.jl (default to `Euclidean()`)
* `neighbors` - Number of neighbors (default to 20% of the data)

### References

Cleveland 1979. *Robust locally weighted regression and smoothing scatterplots.*
"""
@estimsolver LocalWeightRegress begin
  @param weightfun = h -> exp(-3*h^2)
  @param distance = Euclidean()
  @param neighbors = nothing
end

function solve(problem::EstimationProblem, solver::LocalWeightRegress)
  # retrieve problem info
  pdata = data(problem)
  pdomain = domain(problem)

  # result for each variable
  Î¼s = []; Ïƒs = []

  for covars in covariables(problem, solver)
    for var in covars.names
      # get user parameters
      varparams = covars.params[(var,)]

      # get variable type
      V = variables(problem)[var]

      # retrieve non-missing data
      locs = findall(!ismissing, pdata[var])
      ğ’Ÿ = view(pdata, locs)
      X = coordinates(ğ’Ÿ)
      z = ğ’Ÿ[var]

      # number of data points for variable
      ndata = length(z)

      # weight function
      w = varparams.weightfun

      # number of nearest neighbors
      k = isnothing(varparams.neighbors) ? ceil(Int, 0.2ndata) : varparams.neighbors

      @assert 0 < k â‰¤ ndata "invalid number of neighbors"

      # fit search tree
      M = varparams.distance
      if M isa NearestNeighbors.MinkowskiMetric
        tree = KDTree(X, M)
      else
        tree = BruteTree(X, M)
      end

      # pre-allocate memory for results
      varÎ¼ = Vector{V}(undef, nelms(pdomain))
      varÏƒ = Vector{V}(undef, nelms(pdomain))

      # pre-allocate memory for coordinates
      x = MVector{ncoords(pdomain),coordtype(pdomain)}(undef)

      # estimation loop
      for location in traverse(pdomain, LinearPath())
        coordinates!(x, pdomain, location)

        # find neighbors
        inds, dists = knn(tree, x, k)
        Î´s = dists ./ maximum(dists)

        # weighted least-squares
        Wâ‚— = Diagonal(w.(Î´s))
        Xâ‚— = [ones(eltype(X), k) X[:,inds]']
        zâ‚— = view(z, inds)
        Î¸â‚— = Xâ‚—'*Wâ‚—*Xâ‚— \ Xâ‚—'*Wâ‚—*zâ‚—

        # linear combination of response values
        xâ‚’ = [one(eltype(x)); x]
        zÌ‚â‚’ = Î¸â‚— â‹… xâ‚’
        râ‚— = Wâ‚—*Xâ‚—*(Xâ‚—'*Wâ‚—*Xâ‚—\xâ‚’)
        rÌ‚â‚’ = norm(râ‚—)

        varÎ¼[location] = zÌ‚â‚’
        varÏƒ[location] = rÌ‚â‚’
      end

      push!(Î¼s, var => varÎ¼)
      push!(Ïƒs, var => varÏƒ)
    end
  end

  EstimationSolution(pdomain, Dict(Î¼s), Dict(Ïƒs))
end

end
